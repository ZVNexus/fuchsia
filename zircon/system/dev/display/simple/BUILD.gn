# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

source_set("shared") {
  visibility = [ ":*" ]
  sources = [
    "simple-display.cc",
  ]
  deps = [
    "$zx/system/banjo/ddk.protocol.display.controller",
    "$zx/system/banjo/ddk.protocol.pci",
    "$zx/system/dev/lib/device-protocol-pci",
    "$zx/system/dev/lib/mmio",
    "$zx/system/ulib/ddk",
    "$zx/system/ulib/ddktl",
  ]
}

simple_drivers = [
  "bochs",
  "amd-kaveri",
  "nv",
  "intel",
]

group("simple") {
  deps = []
  foreach(driver, simple_drivers) {
    deps += [ ":simple.$driver" ]
  }
  deps += [ ":simple-platform" ]
}

foreach(driver, simple_drivers) {
  driver("simple.$driver") {
    sources = [
      "simple-$driver.c",
    ]
    deps = [
      ":shared",
      "$zx/system/banjo/ddk.protocol.display.controller",
      "$zx/system/banjo/ddk.protocol.pci",
      "$zx/system/dev/lib/device-protocol-pci",
      "$zx/system/ulib/ddk",
    ]
  }
}

config("simple-platform-config") {
  include_dirs = [
    "include",
  ]
}

driver("simple-platform") {
  public_configs = [ ":simple-platform-config" ]
  sources = [
    "simple-platform.cpp",
  ]
  deps = [
    ":shared",
    "$zx/system/banjo/ddk-protocol-display-controller",
    "$zx/system/ulib/ddk",
    "$zx/system/ulib/ddktl",
  ]
}
